/*
 * Radix-4 Booth 64x64 ä¹˜æ³•å™¨
 * è®¾ç½®åŸå›  :
 * é€‚é…æœ‰ç¬¦å·æ•°æ‹“å±•ä¸º65*65ï¼Œæ ¹æ®æŒ‡ä»¤ç±»å‹æˆªå–ç»“æœä¸ºè¾“å‡ºï¼Œç»“æœå…±130ä½æ•°
 * è¾“å…¥ä¸º64ä½è¢«ä¹˜æ•°ä¸ä¹˜æ•°ï¼Œç”±äºæŒ‡ä»¤ç±»å‹ä¸­æœ‰ç¬¦å·ä¹‹åˆ†ï¼Œå› æ­¤æ‰©å±•ä¸€ä½ç”¨äºç¡®å®šç¬¦å·
 * 65ä½çš„è¢«ä¹˜æ•°å†ç¬¦å·æ‰©å±•æˆä¸º130ä½è¢«ä¹˜æ•°
 * 65ä½çš„ä¹˜æ•°ï¼Œç”±äºBoothçš„è¦æ±‚ï¼Œæœ€ä½ä½è¡¥0ï¼Œæœ€é«˜ä½è¿˜éœ€è¦å†è¡¥ç¬¦å·ä½ï¼Œç»“æœæ˜¯67ä½çš„ä¹˜æ•°
 *
 * è®¡ç®—è¿‡ç¨‹ : 
 * æ¯æ¬¡ç”Ÿæˆ130ä½çš„éƒ¨åˆ†ç§¯ï¼Œä¸ç´¯åŠ å¯„å­˜å™¨ç›¸åŠ 
 * ç„¶åä¹˜æ•°å³ç§»ä¸¤ä½ï¼Œè¢«ä¹˜æ•°å·¦ç§»ä¸¤ä½
 *
 * ç»“æŸåˆ¤æ–­ : ä¹˜æ•°ä¸º0ï¼Œåˆ™ä¹˜æ³•è®¡ç®—ç»“æŸ
 */
module ysyx_22050019_multiplier_cycle (
    input clk                  ,
    input rst_n                ,
    input mult_valid           , // ä¹˜æ³•å™¨æ˜¯å¦åˆæ³•è¾“å…¥
    input [4:0] mult_type      , // ä¹˜æ³•ç±»å‹
    input [63:0] multiplicand_i, // è¢«ä¹˜æ•°
    input [63:0] multiplier_i  , // ä¹˜æ•°
    input        result_ready  , // æ˜¯å¦å‡†å¤‡æ¥æ”¶
    output [63:0] mult_out     , // è®¡ç®—ç»“æœ
    output mult_stall          , // è®¡ç®—æš‚åœ 
    output result_ok             // è®¡ç®—ok 
);
//========================================
// ä¹˜æ³•ç±»å‹åˆ¤æ–­
/*
mul    : x[rd] = (x[rs1] Ã— x[rs2])[63:0]
mulh   : x[rd] = (x[rs1]ğ‘  Ã— x[rs2]ğ‘ )[127:64]
mulhu  : x[rd] = (x[rs1]ğ‘¢ Ã— x[rs2]ğ‘¢)[127:64]
mulhsu : x[rd] = (x[rs1]ğ‘  Ã— x[rs2]ğ‘¢)[127:64]
mulw   : x[rd] = sext((x[rs1] Ã— x[rs2])[31: 0])
*/
parameter MUL    = 5'b00001; // ä¹˜
parameter MULH   = 5'b00010; // é«˜ä½ä¹˜
parameter MULHSU = 5'b00100; // é«˜ä½æœ‰ç¬¦å·-æ— ç¬¦å·ä¹˜
parameter MULHU  = 5'b01000; // é«˜ä½æ— ç¬¦å·ä¹˜
parameter MULW   = 5'b10000; // ä¹˜å­—

// çŠ¶æ€æœºå®šä¹‰
parameter IDLE   = 2'b00;
parameter MULTI  = 2'b01;
parameter FINISH = 2'b10;

// è¾“å…¥æ•°æ ¹æ®æŒ‡ä»¤ç±»å‹ç¬¦å·æ‹“å±•
// è¢«ä¹˜æ•°ä¿¡å·å£°æ˜
wire [129:0]multiplicand_trans;
reg  [129:0]multiplicand;

// ä¹˜æ•°ä¿¡å·å£°æ˜
wire [64:0]multiplier_trans;
reg  [66:0]multiplier;

// ç»“æœ
reg [129:0] result;
//========================================
// æ ¹æ®æŒ‡ä»¤ç±»å‹å°†è¾“å…¥ç¬¦å·æ‹“å±•åŠ å…¥è®¡ç®—ä¹˜æ•°å’Œè¢«ä¹˜æ•°ä¸­
assign multiplicand_trans[129:64] = ((mult_type == MULHU) | (mult_type == MULW)) ? 66'b0 : {66{multiplicand_i[63]}};
assign multiplicand_trans[63:32]  = (mult_type == MULW) ? 32'b0 : multiplicand_i[63:32];
assign multiplicand_trans[31:0]   = multiplicand_i[31:0];

assign multiplier_trans  [64]     = ((mult_type == MULW) | (mult_type == MULHSU) | (mult_type == MULHU)) ? 1'b0 : multiplier_i[63];
assign multiplier_trans  [63:32]  = (mult_type == MULW) ? 32'b0 : multiplier_i[63:32];
assign multiplier_trans  [31:0]   = multiplier_i[31:0];

// ä¹˜æ•°è¿›ä¸€æ­¥æ‹“å±•ï¼Œæ ¹æ®boothç®—æ³•ï¼Œä¹˜æ•°éœ€è¦å¶æ•°ï¼Œæ•…æ‹“å±•ä¸€ä¸ªç¬¦å·ä½ï¼Œä½ä½éšå¼è¡¥0
wire [66:0] multiplier_sext;
assign multiplier_sext            = {multiplier_trans[64], multiplier_trans[64:0], 1'b0};

//========================================
// çŠ¶æ€æœºè¿›è¡Œéƒ¨åˆ†ç§¯æ±‚å’Œå¤„ç†
reg [1:0] state, next_state;
reg [4:0] mul_type;
wire [129:0] partial_product;
ysyx_22050019_booth_code #(130)booth_code(
    .multiplicand     ( multiplicand     ),
    .code             ( multiplier[2:0]  ),
    .partial_product  ( partial_product  )
);

// 3æ®µå¼çŠ¶æ€æœºæ„å»ºä¹˜æ³•é€»è¾‘æ¨¡å— 
always@(posedge clk) begin
  if(rst_n)state<=IDLE;
  else   state<=next_state;
end

always @(*) begin
        case(state)
          IDLE  : if(mult_valid) next_state = MULTI ;
                  else           next_state = IDLE  ;
          MULTI : if(~|multiplier) begin
                      next_state = FINISH ;
                  end
                  else next_state = MULTI  ;
          FINISH: if(result_ready)next_state = IDLE ;
                  else next_state = FINISH ;
        default : next_state=IDLE ;
        endcase
end

always @(posedge clk) begin
    if(rst_n) begin
        mul_type     <= 0;
        multiplicand <= 0;
        multiplier   <= 0;
        result       <= 0;
    end
    else begin
        case(state)
          IDLE : if(next_state == MULTI) begin
            mul_type     <= mult_type         ;
            multiplicand <= multiplicand_trans;
            multiplier   <= multiplier_sext   ;
            result       <= 0                 ;
            end
            else begin
            mul_type     <= 0                 ;
            multiplicand <= 0                 ;
            multiplier   <= 0                 ;
            result       <= 0                 ;   
            end
          MULTI: if(next_state == FINISH) begin
            result       <= result            ;
            end
            else begin
            multiplicand <= multiplicand << 2 ;
            multiplier   <= multiplier   >> 2 ;
            result       <= result + partial_product;
            end
          FINISH:if(next_state == IDLE) begin
            mul_type     <= 0                 ;
            multiplicand <= 0                 ;
            multiplier   <= 0                 ;
            result       <= 0                 ; 
            end
            else begin
            mul_type     <= mul_type          ;
            multiplicand <= multiplicand      ;
            multiplier   <= multiplier        ;
            result       <= result            ;   
            end
            default :begin
            end
        endcase
    end
    
end

ysyx_22050019_mux #( .NR_KEY(5), .KEY_LEN(5), .DATA_LEN(64)) ysyx_22050019_mux
(
  .key         (mul_type), //é”®
  .default_out (64'b0),
  .lut         ({		
                 	5'b00001,result[63:0],
				          5'b00010,result[127:64],
				          5'b00100,result[127:64],
				          5'b01000,result[127:64],
				          5'b10000,{{32{result[31]}}, result[31:0]}
                    }), //é”®å’Œè¾“å‡ºçš„è¡¨           
  .out         (mult_out)  //è¾“å‡º
);

//========================================
// è¾“å‡ºæ§åˆ¶
assign result_ok  = (state == FINISH);
assign mult_stall = (state == IDLE && next_state == MULTI) | (state == MULTI);

endmodule

module ysyx_22050019_booth_code #(DATA_WIDTH = 64) (
    input  [DATA_WIDTH-1:0] multiplicand   , //
    input  [2:0]            code           , //
    output [DATA_WIDTH-1:0] partial_product  //
);
/*   B      æ“ä½œ  verilogæè¿°
     000	  0     å…¨0
     001	+ B     +B ={ sign,B} +0
     010	+ B     +B ={ sign,B} +0
     011	+2B     +2B={ B,0}    +0
     100	-2B     -2B={~B,1}    +1
     101	- B     -B ={~sign,~B}+1
     110	- B     -B ={~sign,~B}+1
     111	  0     å…¨0
*/

// è¢«ä¹˜æ³•æ•°ç¬¦å·
wire sign = multiplicand[DATA_WIDTH-1];

// éƒ¨åˆ†ç§¯ç»“æœç”Ÿæˆï¼Œæœ‰ä¹˜æ•°çš„ä½3ä½å†³å®š
wire [DATA_WIDTH:0]op1; //ç¬¦å·ä½æ‹“å±•å’Œè¡¥1ï¼Œå–åç­‰æ“ä½œ
wire op2;               //æ˜¯å¦è¦ï¼‹1çš„è´Ÿæ•°è¡¥ç è¡¥å……

ysyx_22050019_mux #( .NR_KEY(8), .KEY_LEN(3), .DATA_LEN(DATA_WIDTH + 1)) op1_sel
(
  .key         (code), //é”®
  .default_out ({ (DATA_WIDTH + 1){1'b0}}),
  .lut         ({		
                 	3'b000,{ (DATA_WIDTH + 1){1'b0}},
				          3'b001,{ sign, multiplicand    },
				          3'b010,{ sign, multiplicand    },
                 	3'b011,{ multiplicand, 1'b0    },
				          3'b100,{~multiplicand, 1'b1    },
				          3'b101,{~sign, ~multiplicand   },
				          3'b110,{~sign, ~multiplicand   },
				          3'b111,{ (DATA_WIDTH + 1){1'b0}}
                    }), //é”®å’Œè¾“å‡ºçš„è¡¨           
  .out         (op1)  //è¾“å‡º
);

ysyx_22050019_mux #( .NR_KEY(8), .KEY_LEN(3), .DATA_LEN(1)) op2_sel
(
  .key         (code), //é”®
  .default_out (1'b0),
  .lut         ({		
                 	3'b000,1'b0,
				          3'b001,1'b0,
				          3'b010,1'b0,
                 	3'b011,1'b0,
				          3'b100,1'b1,
				          3'b101,1'b1,
				          3'b110,1'b1,
				          3'b111,1'b0
                    }), //é”®å’Œè¾“å‡ºçš„è¡¨           
  .out         (op2)  //è¾“å‡º
);
assign partial_product = op1[DATA_WIDTH-1:0] + {{(DATA_WIDTH-1){1'b0}},op2};//åŠ æ³•è¿™é‡Œå¯ä»¥æŠŠç»“æœæ‰”å¯„å­˜å™¨é‡Œä¸­ç»§ä¸€ä¸‹å†åŠ ï¼Œå¦‚æœè¿™é‡Œå»¶æ—¶å¤ªå¤§çš„è¯å¯ä»¥ç ä¸€åˆ€
endmodule